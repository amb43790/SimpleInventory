#!/usr/bin/perl

use strict;
use warnings;

use DBI;

use Inventory;

my $test_import = 0;
if (@ARGV) { 
    if ($ARGV[0] eq '--test-import') {
        $test_import = 1;
    } else {
        print STDERR "Unknown parameter $ARGV[0]\n";
        exit 1;
    }
}


my $db = Inventory->connect();
unless ($db) {
    print STDERR "Can't create/connect to DB inventory.sqlite3: ".$DBI::errstr."\n";
    exit 1;
}

$SIG{'INT'} = sub {
    print "Exiting without saving changes...\n";
    $db->dbh->rollback;
    exit;
};

$|=1;
while(1) {
    print "Scan...";

    my $barcode = <STDIN>;
    chomp $barcode if $barcode;
    last unless $barcode;

    my $item = $db->lookup_by_barcode($barcode);

    if ($item) {
        $db->adjust_count_by_barcode($barcode,1);

    } else {
        print "\cG";
       if ($test_import) {
           print STDERR "New item barcode $barcode\n";
           next;
        }
        print "*** New item\nSKU: ";
        my $sku = <STDIN>;
        chomp $sku;

        print "Description: ";
        my $desc = <STDIN>;
        chomp $desc;

        $db->create_item(barcode => $barcode,
                         sku => $sku,
                         desc => $desc,
                         count => 1,
                        );
    }
}

if ($test_import) {
    $db->dbh->rollback();
    print "Rolling back from test import...\n";
} else {
    $db->dbh->commit();
    print "Changes saved!\n";
}


