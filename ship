#!/usr/bin/perl

use strict;
use warnings;

use DBI;

use Inventory;

my $test_import = 0;
if (@ARGV) {
    if ($ARGV[0] eq '--test-import') {
        $test_import = 1;
    } else {
        print STDERR "Unknown parameter $ARGV[0]\n";
        exit 1;
    }
}

my $db = Inventory->connect();
unless ($db) {
    print STDERR "Can't create/connect to DB inventory.sqlite3: ".$DBI::errstr."\n";
    exit 1;
}

$SIG{'INT'} = sub {
    print "Exiting without saving changes...\n";
    $db->dbh->rollback;
    exit;
};

$|=1;
while(1) {
    print "Scan...";

    my $barcode = <STDIN>;
    chomp $barcode if $barcode;
    last unless $barcode;

    my $item = $db->lookup_by_barcode($barcode);

    if ($item) {
        printf("sku: %s %s\n",$item->{'sku'}, $item->{'desc'});
        if ($item->{'count'} < 1) {
            print "\cG";
            print "This will make the count negative for barcode $barcode (current count ",$item->{'count'},")\n";
        }

        $db->adjust_count_by_barcode($barcode,-1);
        
    } else {
        print "\cG";
        if ($test_import) {
            print STDERR "New item barcode $barcode\n";
            next;
        }
        print "*** That was an unknown item...\n";

        print "Enter the SKU number: ";
        my $sku = <STDIN>;
        chomp $sku;

        print "Description: ";
        my $desc = <STDIN>;
        chomp $desc;

        print "Quantity on hand (including the one now shipping out): ";
        my $count = <STDIN>;
        chomp $count;
        $count -= 1;

        $db->create_item(barcode => $barcode,
                         sku => $sku,
                         desc => $desc,
                         count => $count,
                        );
    }
}

if ($test_import) {
    $db->dbh->rollback();
    print "Rolling back changes from test import\n";
} else {
    $db->dbh->commit();
    print "Changes saved!\n";
}


